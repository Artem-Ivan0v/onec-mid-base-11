#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Товарная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная'");
	КомандаПечати.Порядок = 5;
	
	// Анкета клиента
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента'");
	КомандаПечати.Порядок = 10;
	
	// Договор (docx)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.Дораб_Доставка";
	КомандаПечати.Идентификатор = "Договор";
	КомандаПечати.Представление = НСтр("ru = 'Договор'");
	КомандаПечати.Порядок = 15;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварнаяНакладная,АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 75;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТоварнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТоварнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Товарная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Дораб_Доставка.ПФ_MXL_ТоварнаяНакладная";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкетыКлиента(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Дораб_Доставка.ПФ_MXL_АнкетаКлиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "Договор");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ОфисныеДокументы = ПечатьДоговора(МассивОбъектов);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Договор'");
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьТоварнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТоварнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Дораб_Доставка.ПФ_MXL_ТоварнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет, "Товарная накладная");

		ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет);

		ВывестиПодвалТоварнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьАнкетыКлиента(МассивОбъектов, ОбъектыПечати)
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаКлиента";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Дораб_Доставка.ПФ_MXL_АнкетаКлиента");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет, "Анкета о доставке");
		
		ВывестиОценкуТовара(ТабличныйДокумент, Макет);
		
		ВывестиКомментарий(ТабличныйДокумент, Макет);
		
		ВывестиОценкуДоставки(ТабличныйДокумент, Макет);
		
		ВывестиКомментарий(ТабличныйДокумент, Макет);
		
		ВывестиРекомендацию(ТабличныйДокумент, Макет);
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Дораб_Доставка.Ссылка КАК Ссылка,
	               |	Дораб_Доставка.Номер КАК Номер,
	               |	Дораб_Доставка.Дата КАК Дата,
	               |	Дораб_Доставка.Организация КАК Организация,
	               |	Дораб_Доставка.Контрагент КАК Контрагент,
	               |	Дораб_Доставка.Договор КАК Договор,
				   |	Дораб_Доставка.АдресДоставки КАК АдресДоставки,
				   |	Дораб_Доставка.Основание КАК Основание,
	               |	Дораб_Доставка.Ответственный КАК Ответственный,
				   |	Дораб_Доставка.Комментарий КАК Комментарий,
	               |	Дораб_Доставка.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |		Количество КАК Количество
	               |	) КАК Товары,
	               |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваровУслуг.Дораб_КонтактноеЛицо КАК Дораб_КонтактноеЛицо,
	               |	Дораб_Доставка.Контрагент.ИНН КАК КонтрагентИНН,
	               |	Дораб_Доставка.Контрагент.КПП КАК КонтрагентКПП,
	               |	Дораб_Доставка.Контрагент.Ссылка КАК КонтрагентСсылка,
	               |	Дораб_Доставка.Контрагент.Наименование КАК КонтрагентНаименование,
	               |	Дораб_Доставка.Организация.ИНН КАК ОрганизацияИНН,
	               |	Дораб_Доставка.Организация.КПП КАК ОрганизацияКПП,
	               |	Дораб_Доставка.Организация.КонтактнаяИнформация.(
	               |		Ссылка КАК ОрганизацияСсылка,
	               |		НомерСтроки КАК ОрганизацияНомерСтроки,
	               |		Тип КАК ОрганизацияТип,
	               |		Вид КАК ОрганизацияВид,
	               |		Представление КАК ОрганизацияПредставление,
	               |		ЗначенияПолей КАК ОрганизацияЗначенияПолей,
	               |		Страна КАК ОрганизацияСтрана,
	               |		Регион КАК ОрганизацияРегион,
	               |		Город КАК ОрганизацияГород,
	               |		АдресЭП КАК ОрганизацияАдресЭП,
	               |		ДоменноеИмяСервера КАК ОрганизацияДоменноеИмяСервера,
	               |		НомерТелефона КАК ОрганизацияНомерТелефона,
	               |		НомерТелефонаБезКодов КАК ОрганизацияНомерТелефонаБезКодов,
	               |		ВидДляСписка КАК ОрганизацияВидДляСписка,
	               |		Значение КАК ОрганизацияЗначение
	               |	) КАК Организация1
	               |ИЗ
	               |	Документ.Дораб_Доставка КАК Дораб_Доставка
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |		ПО Дораб_Доставка.Основание = РеализацияТоваровУслуг.Ссылка
	               |ГДЕ
	               |	Дораб_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ПечатьДоговора(МассивОбъектов)
	
	ОфисныеДокументы = Новый Соответствие;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Дораб_Доставка.ПФ_DOC_Договор");
	
	МакетОфисный = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(Макет, Неопределено);
	
	ОписаниеОбластей = Новый Структура;
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Шапка", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Тело", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал", "Общая");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	Пока ДанныеДокументов.Следующий() Цикл
		ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, МакетОфисный);
		ЗначенияПараметров = Новый Структура;

		ЗначенияПараметров.Вставить("Дата", ДанныеДокументов.Дата);
		ЗначенияПараметров.Вставить("Организация", ДанныеДокументов.Организация);
		ЗначенияПараметров.Вставить("Ответственный", ДанныеДокументов.Ответственный);
		ЗначенияПараметров.Вставить("Контрагент", ДанныеДокументов.Контрагент);
		ЗначенияПараметров.Вставить("СуммаДок", ДанныеДокументов.СуммаДокумента);
		ЗначенияПараметров.Вставить("АдресДоставки", ДанныеДокументов.АдресДоставки);
		ЗначенияПараметров.Вставить("КонтактноеЛицо", ДанныеДокументов.Дораб_КонтактноеЛицо);
		ЗначенияПараметров.Вставить("ИНН", ДанныеДокументов.КонтрагентИНН);
		ЗначенияПараметров.Вставить("КПП", ДанныеДокументов.КонтрагентКПП);
		
		Область = УправлениеПечатью.ОбластьМакета(МакетОфисный, ОписаниеОбластей.Шапка);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ЗначенияПараметров);
		
		Область = УправлениеПечатью.ОбластьМакета(МакетОфисный, ОписаниеОбластей.Тело);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ЗначенияПараметров);
		
		Область = УправлениеПечатью.ОбластьМакета(МакетОфисный, ОписаниеОбластей.Подвал);
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ЗначенияПараметров);
		
		АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
		
		ОфисныеДокументы.Вставить(АдресХранилищаПечатнойФормы, Строка(ДанныеДокументов.Ссылка));
		УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
	КонецЦикла;
	УправлениеПечатью.ОчиститьСсылки(МакетОфисный);
	
	Возврат	ОфисныеДокументы;
КонецФункции

Процедура ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет, Шаблон)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("Заголовок");
	
	ДанныеПечати = Новый Структура;
	
	Если Шаблон = "Товарная накладная" Тогда
		ШаблонЗаголовка = "Товарная накладная №%1 от %2";
	ИначеЕсли Шаблон = "Анкета о доставке" Тогда
		ШаблонЗаголовка = "Анкета о доставке №%1 от %2";
	КонецЕсли;
	
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ДанныеПечати.Вставить("Грузоотправитель", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Грузополучатель", ДанныеДокументов.Контрагент);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
	
	НавСсылка = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(НавСсылка, 1, 120);
	
	Если НЕ ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
	    ТекстСообщения = НСтр("ru = 'Не удалось сформировать QR-код.
	                |Технические подробности см. в журнале регистрации.'");
	    ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	Иначе
	    КартинкаQRКода = Новый Картинка(ДанныеQRКода);
	    ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаQRКода;
	КонецЕсли;		
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
КонецПроцедуры

Процедура ВывестиТоварыУслуги(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
		
КонецПроцедуры

Процедура ВывестиПодвалТоварнойНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвалЗаказа = Макет.ПолучитьОбласть("ОбластьПодвал");

	ТабличныйДокумент.Вывести(ОбластьПодвалЗаказа);
	
КонецПроцедуры

Процедура ВывестиОценкуТовара(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("ОценкаТовара");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

Процедура ВывестиКомментарий(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("Комментарий");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

Процедура ВывестиОценкуДоставки(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("ОценкаДоставки");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

Процедура ВывестиРекомендацию(ТабличныйДокумент, Макет)
	
	Область = Макет.ПолучитьОбласть("Рекомендация");

	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли







